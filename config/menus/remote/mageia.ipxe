#!ipxe

# Mageia Operating System
# http://www.mageia.org/

# No way to set the network interface by MAC address, let the installer ask the question
isset ${dhcp-server} && set network dhcp || set network static,ip:${ip},netmask:${netmask},gateway:${gateway},dns:${dns}

goto ${menu} ||

:mageia
set os Mageia
menu ${os} - ${arch} - Image Sig Checks: [${img_sigs_enabled}]
item 7.1 ${space} ${os} 7.1
item cauldron ${space} ${os} cauldron
choose version || goto mageia_exit
goto mageia_boot

:mageia_boot
set dir ${mageia_base_dir}/distrib/${version}/x86_64 && set dir2 isolinux/x86_64 ||
iseq ${arch} i386 && set dir ${mageia_base_dir}/distrib/${version}/i586 && set dir2 isolinux/i386 ||
set automatic method:http,network:${network},server:${mageia_mirror},directory:/${dir}
imgfree
kernel ${mageia_mirror}/${dir}/${dir2}/vmlinuz
initrd ${mageia_mirror}/${dir}/${dir2}/all.rdz
imgargs vmlinuz automatic=${automatic} vga=788 splash=silent ${console} initrd=all.rdz
echo
echo MD5sums:
md5sum vmlinuz all.rdz
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify vmlinuz ${sigs}${dir}/${dir2}/vmlinuz.sig || goto error
imgverify all.rdz ${sigs}${dir}/${dir2}/all.rdz.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot || goto mageia

:mageia_exit
clear menu
exit 0
