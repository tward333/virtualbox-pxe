#!ipxe

goto ${menu} ||

:linux_menu
menu Linux Installers - Current Arch [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap Linux Distros:
item alpinelinux ${space} Alpine Linux
item archlinux ${space} Arch Linux
item centos ${space} CentOS
item flatcar ${space} Container Linux by Flatcar
item debian ${space} Debian
item fedora ${space} Fedora
item ipfire ${space} IPFire
item mageia ${space} Mageia
item nixos ${space} NixOS
item opensuse ${space} OpenSUSE
item rancheros ${space} RancherOS
item scientific ${space} Scientific Linux
item slackware ${space} Slackware
item tinycore ${space} Tiny Core Linux
item ubuntu ${space} Ubuntu
choose menu || goto linux_exit
echo ${cls}
goto ${menu} ||
iseq ${sigs_enabled} true && goto verify_sigs || goto change_menu

:verify_sigs
imgverify ${menu}.ipxe ${sigs}${menu}.ipxe.sig || goto error
goto change_menu

:change_menu
chain ${menu}.ipxe || goto error
goto linux_menu

:linux_exit
clear menu
exit 0
